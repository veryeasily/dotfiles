" File specifically for integration between VIM and FZF

" Borrowed from
" [here](https://medium.com/@crashybang/supercharge-vim-with-fzf-and-ripgrep-d4661fc853d2)
" --column: Show column number
" --line-number: Show line number
" --no-heading: Do not show file headings in results
" --fixed-strings: Search term as a literal string
" --ignore-case: Case insensitive search
" --hidden: Search hidden files and folders
" --follow: Follow symlinks
" --glob: Additional conditions for search (in this case ignore everything in the .git/ folder)
" --color: Search color options
command! -bang -nargs=* Find call fzf#vim#grep('rg --column --line-number --no-heading --fixed-strings --ignore-case --hidden --follow --glob "!.git/*" --color "always" '.shellescape(<q-args>), 1, <bang>0)

command! -bar -bang MapsAll call fzf#vim#maps("", <bang>0)

command! -bang -nargs=* Rg
  \ call fzf#vim#grep(
  \   'rg --column --line-number --no-heading --color=always '.shellescape(<q-args>), 1,
  \   <bang>0 ? fzf#vim#with_preview('up:60%')
  \           : fzf#vim#with_preview('right:50%:hidden', '?'),
  \   <bang>0)

command! -bang -nargs=* FindFd
  \ call fzf#run({
  \   'source':
  \     split(system("rg --follow --files --hidden --glob '!.git/'"), "\n"),
  \   'sink':    'vs',
  \   'down':    '40%'
  \  })

command! -bang -nargs=* FindFdTwo
  \ call fzf#run({
  \   'source':
  \     split(system("rg --follow --files --hidden --glob '!.git/'"), "\n"),
  \   'sink':    'sp',
  \   'down':    '40%'
  \  })

nnoremap <silent> <Leader>C :call fzf#run({
      \   'source':
      \     map(split(globpath(&rtp, "colors/*.vim"), "\n"),
      \         "substitute(fnamemodify(v:val, ':t'), '\\..\\{-}$', '', '')"),
      \   'sink':    'colo',
      \   'options': '+m',
      \   'left':    30
      \ })<CR>

command! FZFMru call fzf#run({
\  'source':  v:oldfiles,
\  'sink':    'e',
\  'options': '-m -x +s',
\  'down':    '40%'})

noremap <leader>g <Esc>:<c-u>Find<cr>
noremap <leader>l <Esc>:<c-u>Lines<cr>
" noremap <leader>t <Esc>:<c-u>FZF<CR>
noremap <leader>u <Esc>:<c-u>History<CR>
" noremap <leader>t <Esc>:<c-u>FindFd<CR>
noremap <leader>T <Esc>:<c-u>FindFdTwo<CR>
noremap <leader>o <Esc>:<c-u>FZFMru<CR>

function! FzyCommand(choice_command, vim_command)
  try
    let output = system(a:choice_command . " | fzy ")
  catch /Vim:Interrupt/
    " Swallow errors from ^C, allow redraw! below
  endtry
  redraw!
  if v:shell_error == 0 && !empty(output)
    exec a:vim_command . ' ' . output
  endif
endfunction

nnoremap <leader>fe :call FzyCommand("fd --no-ignore-vcs --type file --hidden", ":e")<cr>
nnoremap <leader>fv :call FzyCommand("fd --no-ignore-vcs --type file --hidden", ":vs")<cr>
nnoremap <leader>fx :call FzyCommand("fd --no-ignore-vcs --type file --hidden", ":sp")<cr>
